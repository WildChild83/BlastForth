( ---------------------------------------------------------------------------- )
( **************************************************************************** )
(       Default Vertical and Horizontal Blank Handlers                         )
( **************************************************************************** )
( ---------------------------------------------------------------------------- )

( ---------------------------------------------------------------------------- )
(                                                                              )
(       Dependencies:                                                          )
(           - glossary.fs                                                      )
(           - romfile.fs                                                       )
(           - 68k.fs                                                           )
(           - forth.fs                                                         )
(           - "next&" must be defined                                          )
(                                                                              )
( ---------------------------------------------------------------------------- )
Forth definitions

( ---------------------------------------------------------------------------- )
( **************************************************************************** )
(       Vblank Next                                                            )
( **************************************************************************** )
( ---------------------------------------------------------------------------- )
(           jump to the current vblank handler and resume normal execution     )
( ---------------------------------------------------------------------------- )
variable vblank         \ code field address of software vblank handler

asm data vbnext& ( -- )
    next& [#] np lea,   \ restore NP to "standard next"
    vblank [#] a4 move, \ A4 = code field address [from vblank variable]
    [a4]+ a3 move,      \ A3 = code field,  A4 = data field address
    [a3] jump,          \ jump to code field
    end

( ---------------------------------------------------------------------------- )
( **************************************************************************** )
(       Vertical Blank Handler  [hardware]                                     )
( **************************************************************************** )
( ---------------------------------------------------------------------------- )
(           switch from "standard next" to "vblank next"                       )
( ---------------------------------------------------------------------------- )
hvariable vbi           \ counts how many VBIs have occurred

asm 68k-vbi:            \ CPU vector table points here
    vbi [#] h inc,      \ increment VBI counter
    vbnext& [#] np lea, \ point NP to "vblank next"
    ireturn,            \ all done!
    end

( ---------------------------------------------------------------------------- )
( **************************************************************************** )
(       Horizontal Blank Handler  [hardware]                                   )
( **************************************************************************** )
( ---------------------------------------------------------------------------- )
(           jump directly to raw assembly routine  [Forth code is too slow]    )
( ---------------------------------------------------------------------------- )
variable hblank         \ address of asm-only hblank handler
                        \ ...which must end with an Ireturn,

asm 68k-hbi:            \ CPU vector table points here
    hblank [#] rpush,   \ push address of hblank handler onto Return Stack
    return,             \ ...and jump to the address we just pushed
    end

( ---------------------------------------------------------------------------- )
( ---------------------------------------------------------------------------- )
( ---------------------------------------------------------------------------- )
( ---------------------------------------------------------------------------- )
( ---------------------------------------------------------------------------- )





















